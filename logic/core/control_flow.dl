// Copyright (c) 2023 Google LLC
//
// Permission is hereby granted, free of charge, to any person obtaining a copy of
// this software and associated documentation files (the "Software"), to deal in
// the Software without restriction, including without limitation the rights to
// use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of
// the Software, and to permit persons to whom the Software is furnished to do so,
// subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS
// FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR
// COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
// IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
// CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

#pragma once

#include "defines.dl"
#include "input/control_flow.dl"

#ifdef DEBUG_CFG
#define CF_OUTPUT(x) OUTPUT_IMPL(x)
#else
#define CF_OUTPUT(x)
#endif


.comp _ControlFlowGraph {

  .decl block_predecessor(fid: func_t, bid: block_t, bid_pred: block_t)
  CF_OUTPUT(block_predecessor)

  block_predecessor(fid, bid, bid_pred) :-
    block_pred(_, fid, bid, bid_pred).

  block_predecessor(fid, bid, bid_pred) :-
    block_pred(_, fid, bid, tmp),
    block_predecessor(fid, tmp, bid_pred).

  .decl block_successor(fid: func_t, A: block_t, B: block_t)
  CF_OUTPUT(block_successor)

  block_successor(fid, A, B) :-
    block_succ(_, fid, A, B).

  block_successor(fid, A, B) :-
    block_succ(_, fid, A, tmp),
    block_successor(fid, tmp, B).

  .decl block_successor_without_node(fid: func_t,
                                     A: block_t,
                                     B: block_t,
                                     C: block_t)  // exclude path with this node in it
  CF_OUTPUT(block_successor_without_node)

  block_successor_without_node(fid, A, B, C) :-
    block_succ(_, fid, C, _),
    block_succ(_, fid, A, B),
    C != B.

  block_successor_without_node(fid, A, B, C) :-
    block_succ(_, fid, A, tmp),
    C != tmp,
    block_successor_without_node(fid, tmp, B, C).

  // A dom B
  .decl block_dom(fid: func_t, A: block_t, B: block_t)
  CF_OUTPUT(block_dom)

  block_dom(fid, A, B) :-
    block_idom(_, fid, B, A).

  block_dom(fid, A, B) :-
    block_idom(_, fid, B, tmp),
    block_dom(fid, A, tmp).


  .decl block_forward_egdes(fid: func_t, A: block_t, B: block_t)
  CF_OUTPUT(block_forward_egdes)

  block_forward_egdes(fid, A, B) :-
    block_forward_egde(fid, A, B).

  block_forward_egdes(fid, A, B) :-
    block_forward_egde(fid, A, tmp),
    block_forward_egdes(fid, tmp, B).


  .decl block_forward_egde(fid: func_t, A: block_t, B: block_t)
  CF_OUTPUT(block_forward_egde)

  block_forward_egde(fid, A, B) :-
    block_succ(_, fid, A, B),
    !block_dom(fid, B, A).


  .decl block_back_egde(fid: func_t, A: block_t, B: block_t)
  CF_OUTPUT(block_back_egde)

  block_back_egde(fid, A, B) :-
    block_succ(_, fid, A, B),
    block_dom(fid, B, A).

  // Natural loop nodes
  .decl block_loop_node(fid: func_t, head: block_t, node: block_t)
  CF_OUTPUT(block_loop_node)

  block_loop_node(fid, head, node) :-
    block_back_egde(fid, tail, head),
    block_successor_without_node(fid, node, tail, head),
    block_dom(fid, head, node).

  block_loop_node(fid, head, node) :-
    block_back_egde(fid, tail, head),
    node = tail.

  block_loop_node(fid, head, node) :-
    block_back_egde(fid, _, head),
    node = head.
}

.init CFG = _ControlFlowGraph
